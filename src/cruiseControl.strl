% Module which manages the state of the cruise control system to follow the
% behavioural requirements.
module StateMachine:
    % Constants defined in CruiseControl.h
    constant SPEED_MIN : float, SPEED_MAX : float, PEDALS_MIN : float;
    constant OFF : integer, ON : integer, STDBY : integer, DISABLE : integer;

    % Interface variables
    input On1, Off1, Resume1, Accel1 : float, Brake1: float, Speed1 : float;
    output CruiseState1 : integer;

    var state := OFF : integer in
        loop
            % Emit current state
            emit CruiseState1(state);
            pause;
            
            % Calculate next state
            trap TState in
                if state = OFF then
                    % Go on when the on button is pressed
                    present On1 then
                        state := ON;
                        exit TState;
                    end present;
                elsif state = ON then
                    % Go off when the off button is pressed
                    present Off1 then
                        state := OFF;
                        exit TState;
                    end present;

                    % Go standby when the brake is pressed
                    if ?Brake1 > PEDALS_MIN then
                        state := STDBY;
                    % Disable when accelerator pressed or outside of speed limits
                    elsif ?Accel1 > PEDALS_MIN or ?Speed1 < SPEED_MIN or ?Speed1 > SPEED_MAX then
                        state := DISABLE;
                    end if;
                elsif state = STDBY then
                    % Go off when the off button is pressed
                    present Off1 then
                        state := OFF;
                        exit TState;
                    end present;

                    % Exit standby when the resume button is pressed
                    present Resume1 then
                        % Disable when accelerator pressed or outside of speed limits, otherwise go on
                        if ?Accel1 > PEDALS_MIN or ?Speed1 < SPEED_MIN or ?Speed1 > SPEED_MAX then
                            state := DISABLE;
                        else
                            state := ON;
                        end if;
                    end present;
                elsif state = DISABLE then
                    % Go off when the off button is pressed
                    present Off1 then
                        state := OFF;
                        exit TState;
                    end present;

                    % Go standby when the brake is pressed
                    if ?Brake1 > PEDALS_MIN then
                        state := STDBY;
                    % Go on when accelerator not pressed and within speed limits
                    elsif ?Accel1 <= PEDALS_MIN and ?Speed1 >= SPEED_MIN and ?Speed1 <= SPEED_MAX then
                        state := ON;
                    end if;
                end if;
            end trap
        end loop
    end var
end module

module CarDrivingControl:
    % Constants defined in CruiseControl.h
    constant OFF : integer, ON : integer, STDBY : integer, DISABLE : integer;

    % Functions defined in CruiseControl.c
	function regulateThrottle(boolean, float, float) : float;

    % Interface variables
	input CruiseState1 : integer, Accel1 : float, Speed1 : float, CruiseSpeed1 : float;
	output ThrottleCmd1 : float;

	loop
		if ?CruiseState1 = ON then
			if (?CruiseState1 = ON and pre(?CruiseState1) = OFF) then
				emit ThrottleCmd1(regulateThrottle(true, ?CruiseSpeed1, ?Speed1));
			else 
				emit ThrottleCmd1(regulateThrottle(false, ?CruiseSpeed1, ?Speed1));
			end if;
		else
			emit ThrottleCmd1(?Accel1);
		end if;

		pause;
	end loop
end module

% Module which manages the cruise speed.
module CruiseSpeedManagement:
    % Constants defined in CruiseControl.h
	constant SPEED_MIN : float, SPEED_MAX : float, SPEED_INC : float;
    constant OFF : integer, ON : integer, STDBY : integer, DISABLE : integer;

	% Interface variables
	input On1, Set1, QuickDecel1, QuickAccel1, Speed1 : float, CruiseState1 : integer;
	output CruiseSpeed1 : float;
	
	% temporary speed variable
	var temp_Speed := 0.0f : float in
		loop
			emit CruiseSpeed1(temp_Speed);
			pause;
			
			trap cruiseManage_T in
				% Set cruising speed when on button is pressed for the first time
				present On1 then
					if ?CruiseState1 = ON and temp_Speed = 0.0f then
						temp_Speed := ?Speed1;

						% Limit speed to maximum and minimum speed
						if (temp_Speed > SPEED_MAX) then
							temp_Speed := SPEED_MAX;
						elsif (temp_Speed < SPEED_MIN) then
							temp_Speed := SPEED_MIN;
						end if;
						exit cruiseManage_T;
					end if;
				end present;
				
				% Stop managing cruise speed when cruise control is off
				if ?CruiseState1 = OFF then
					temp_Speed := 0.0f;
					exit cruiseManage_T;
				end if;
				
				% Set cruising speed when set button is pressed
				present Set1 then
					temp_Speed := ?Speed1;

					% Limit speed to maximum and minimum speed
					if (temp_Speed > SPEED_MAX) then
						temp_Speed := SPEED_MAX;
					elsif (temp_Speed < SPEED_MIN) then
						temp_Speed := SPEED_MIN;
					end if;
					exit cruiseManage_T;
				end present;
				
				% Accelerate when quick acceleration button is pressed
				present pre(QuickAccel1) then
					temp_Speed := temp_Speed + SPEED_INC;

					% Limit speed to maximum speed
					if (temp_Speed > SPEED_MAX) then
						temp_Speed := SPEED_MAX;
					end if;
					exit cruiseManage_T;
				end present;
				
				% Decelerate when quick deceleration button is pressed
				present pre(QuickDecel1) then
					temp_Speed := temp_Speed - SPEED_INC;

					% Limit speed to minimum speed
					if (temp_Speed < SPEED_MIN) then
						temp_Speed := SPEED_MIN;
					end if;
					exit cruiseManage_T;
				end present;
			end trap
		end loop
	end var
end module

% Top level module for the cruise control system that runs three modules in
% parallel.
module CruiseControl:
    % Interface variables
    input On, Off, Resume, Set, QuickDecel, QuickAccel;
	input Accel := 0.0f : float, Brake := 0.0f: float, Speed := 0.0f : float;
    output CruiseSpeed : float, ThrottleCmd : float, CruiseState : integer;

    % Run modules
    signal state : integer in
        loop
			emit CruiseState(?state);
            pause
        end loop
        ||
        run StateMachine[
			signal On/On1;
			signal Off/Off1;
			signal Resume/Resume1;
			signal Accel/Accel1;
			signal Brake/Brake1;
			signal Speed/Speed1;
			signal state/CruiseState1
		]
        ||
        run CarDrivingControl[
            signal state/CruiseState1;
            signal Accel/Accel1;
            signal Speed/Speed1;
            signal CruiseSpeed/CruiseSpeed1;
            signal ThrottleCmd/ThrottleCmd1
		]
        ||
        run CruiseSpeedManagement[
			signal On/On1;
			signal Set/Set1;
			signal QuickDecel/QuickDecel1;
			signal QuickAccel/QuickAccel1;
			signal Speed/Speed1;
			signal state/CruiseState1;
			signal CruiseSpeed/CruiseSpeed1
		]
    end signal
end module